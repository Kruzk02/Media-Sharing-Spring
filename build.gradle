plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.6'

    id 'jacoco'                                    // for code coverage
    id 'com.diffplug.spotless' version '8.0.0'    // for formatting
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // --- Spring Boot Starters ---
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // --- Jackson ---
    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'

    // --- OpenAPI ---
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'

    // --- Utilities ---
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'
    implementation 'org.modelmapper:modelmapper:3.2.4'
    implementation 'org.apache.commons:commons-lang3'

    // --- JWT ---
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    implementation 'io.jsonwebtoken:jjwt-impl:0.13.0'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.13.0'

    // --- Database ---
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'org.springframework.boot:spring-boot-docker-compose'

    // --- Lombok ---
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // --- Testing ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'junit:junit'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'com.redis:testcontainers-redis:2.2.4'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.21.3"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.test {
    useJUnitPlatform()
}

// Spotless config (like Maven plugin)
spotless {
    java {
        googleJavaFormat()
    }
}

// Jacoco report config
jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}
